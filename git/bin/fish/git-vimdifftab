#!/usr/bin/fish

function usage
    set -l usage \
"
USAGE:
    "(basename (status --current-filename))" [OPTION] COMMIT [PATH...]
OPTIONS:
    -f, --fzf    Select files using fzf
    -h, --help   Display this help
"
    echo $usage
end

argparse --min-args=1 'h/help' 'f/fzf' -- $argv; or begin
    usage
    exit 1
end

set -q _flag_help; and begin
    usage
    exit
end

set commitish $argv[1]

test (count $argv) -ge 0

set diff_files (git diff --name-only --relative $commitish $argv[2..])
test (count $diff_files) -eq 0; and begin
    echo no diff found
    exit 0
end

test (count $diff_files) -ge 10; and set _flag_fzf '-f'

set -q _flag_fzf; and begin
    set diff_files (string split ' ' $diff_files | fzf)
    test (count $diff_files) -eq 0; and exit 0
end

echo $diff_files | xargs nvim -p -c "tabdo Gdiffsplit $commitish"
